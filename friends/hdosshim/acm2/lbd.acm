$LBD    SPACE   4,10
***     $LBD - LOOKUP BAUDRATE DIVISOR.
*
*       $LBD TRANSLATES A BAUD RATE INTO THE PROPER DIVISOR FOR THE
*       8250 CHIPS ON THE H8-4 SERIAL CARD.
*
*       NOTE THAT $LBD DOES NOT ACTUALLY COMPUTE THE TRANSFORMATION, BUT
*       SIMPLY LOOKS UP THE VALUE IN A TABLE. THIS IS DONE TO DETECT TYPOS
*       IN THE USER SUPPLIED BAUD RATE.
*
*       ENTRY   (DE) = BAUD RATE (AS A BINARY NUMBER)
*       EXIT    'Z' SET IF VALID BAUD RATE
*                (HL) = DIVISOR
*               'Z' CLEAR IF NOT VALID BAUD RATE
*       USES    A,F,D,E,H,L


$LBD    MOV     A,D
        ORA     E               (A) = CODE VALUE
        LXI     H,LBDA          (HL) = LOOKUP TABLE
        CALL    $WTBLS          WORD TABLE LOOKUP
        MOV     A,M
        INX     H
        MOV     H,M
        MOV     L,A
        RET                     RETURN WITH CONDITION CODE FROM $WTBLS


**      BAUD RATE VS 8250 DIVISOR TABLE.
*
*       KEY IS BAUD RATE SQUEEZED INTO ONE BYTE

LBDA    DS      0
        DB      151Q            2400/256+#2400
        DW      000060A
        DB      245Q            9600/256+#9600
        DW      000014A
        DB      132Q            600/256+#600
        DW      000300A
        DB      113Q            19200/256+#19200
        DW      000006A
        DB      322Q            4800/256+#4800
        DW      000030A
        DB      264Q            1200/256+#1200
        DW      000140A
        DB      055Q            300/256+#300
        DW      001200A
        DB      074Q            7200/256+#7200
        DW      000020A
        DB      036Q            3600/256+#3600
        DW      000040A
        DB      017Q            1800/256+#1800
        DW      000100A
        DB      156Q            110/256+#110
        DW      204027A
        DB      226Q            150/256+#150
        DW      003000A
        DB      0               END OF TABLE
