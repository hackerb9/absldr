* * *	MFINIT sets up the mini-floppy volume parameters, optionally
*	initializes the volume with sector headers, and optionally
*	performs a media check.
 
*	Copyright 1981 UltiMeth Corp.
 
MFISEEK	SET	MFISEEK		;MAKE MFISEEK DEFINED
MFISKEW	SET	MFISEEK/20+2	;SET INTERTRACK SKEW FACTOR
MFISTEP	SET	MFISKEW-2*20+14  ;SET SEEK STEP TIME (MS) FOR FORMATTING
 
MFINIT	CALL	$$VSN		;GET VOLUME SERIAL NUMBER
	MOV	C,A		;SAVE NEW VOLUME ID
	LHLD	D.SDP+1		;GET BEGINNING OF SET-DEVICE-PARAMETERS ROUTINE
	INX	HL		;GET MFUTBL POINTER LOC
	MOV	E,M
	INX	HL
	MOV	D,M		;GET MFUTBL LOC
	LHLD	AIO.UNI		;GET UNIT NUMBER IN (L)
	MVI	H,0		;CLEAR (H)
	DAD	HL		;COMPUTE UNIT TABLE OFFSET
	DAD	DE		;COMPUTE UNIT TABLE ENTRY LOC
	MOV	A,M		;GET UNIT BITS
	RRC
	RRC
	RRC
	RRC			;GET POSSIBLE MEDIA BITS
	ANI	VFL.80T+VFL.2SD  ;GET MEDIA CONFIGURATION BITS
	MOV	B,A		;SAVE POSSIBLE MEDIA BITS
	PUSH	DE		;SAVE DEVICE DRIVER UNIT TABLE LOC
	PUSH	BC		;SAVE POSSIBLE MEDIA BITS, NEW VOLUME ID
	ANI	VFL.2SD		;IS UNIT DOUBLE SIDED -
	JZ	MFISAME		;- NO, SKIP
 
MFI2SID	CALL	$$TYPTX
	DB	NL,'Double sided <YES>?',' '+80H
	CALL	$$ITL.
	JC	MFI2SID		;REPEAT IF CTL-D
 
	MOV	A,M
	ANA	A		;DEFAULT -
	JZ	MFISAME		;- YES, SKIP
 
	CALL	$$CYS		;'YES' -
	JE	MFISAME		;- YES, SKIP
 
	CALL	$$CNO		;'NO' -
	JNE	MFI2SID		;- NO, REPEAT
 
	POP	BC		;RESTORE POSSIBLE MEDIA BITS, NEW VOLUME ID
	MOV	A,B		;GET POSSIBLE MEDIA BITS
	ANI	-1-VFL.2SD	;SET SINGLE SIDE ONLY
	MOV	B,A		;SAVE MEDIA BITS
	PUSH	BC		;SAVE MEDIA BITS, NEW VOLUME ID
 
MFISAME	POP	BC		;GET MEDIA BITS, NEW VOLUME SERIAL NUMBER
	MOV	A,B		;GET MEDIA BITS
	STA	VOLFLG		;SET MEDIA BITS IN VOLUME PARAMETER BLOCK
	LXI	HL,40		;GET TRACKS ON BASIC DISK
	ANA	A		;BASIC DISK -
	JZ	MFI1SID		;- YES, SKIP
 
	DAD	HL		;- YES, DOUBLE TRACK NUMBER
 
	CPI	VFL.80T+VFL.2SD  ;80-TRACK, 2-SIDED -
	JNE	MFI1SID		;- NO, SKIP
 
	DAD	HL		;- YES, DOUBLE TRACK NUMBER
 
MFI1SID	MOV	E,L
	MOV	D,H		;GET TRACKS ON DISK
	DAD	HL		;*2
	DAD	HL		;*4
	DAD	DE		;*5
	DAD	HL		;*10
	SHLD	VOLSIZ		;SET VOLUME SIZE IN SECTORS INTO VOLUME BLOCK
 
	POP	HL		;GET DEVICE DRIVER UNIT TABLE LOC
	MOV	D,C		;GET VOLUME ID
	PUSH	BC		;SAVE MEDIA BITS, VOLUME ID FOR LABEL WRITE
	PUSH	DE		;SAVE VOLUME ID, NUMBER OF TRACKS
	PUSH	BC		;SAVE MEDIA BITS, VOLUME ID
	LXI	DE,MFUTBL-.	;GET BOOT UNIT TABLE LOC
	MVI	C,3		;GET MAX UNIT COUNT
 
MFILP1	MOV	A,M		;GET UNIT BITS
	ANI	VFL.80T+VFL.2SD*10000B  ;GET UNIT CONFIGURATION BITS
	ORA	B		;UPDATE MEDIA BITS IN UNIT ENTRY
	MOV	M,A		;SET UNIT BITS IN DEVICE DRIVER TABLE
	STAX	DE		;SET UNIT BITS IN BOOT TABLE
	INX	HL
	INX	DE
	MOV	A,M
	STAX	DE		;COPY SEEK STEP RATE TO BOOT TABLE
	INX	HL
	INX	DE
	DCR	C		;ANY MORE UNITS -
	JNZ	MFILP1		;- YES, LOOP
 
	CALL	D.ABORT		;RESET DEVICE TO TRACK ZERO
 
	LXI	BC,256
	LHLD	$$BUFF		;GET BUFFER LOC
	XCHG
	PUSH	DE		;SAVE BUFFER LOC
	LXI	HL,9
	CALL	D.READR		;GO READ LABEL SECTOR IF PRESENT
	POP	DE		;RESTORE BUFFER LOC
	POP	BC		;RESTORE MEDIA BITS, NEW VOLUME ID
	JC	MFIFMT		;ERROR, GO FORMAT
 
	MOV	A,C		;GET NEW VOLUME ID
	LXI	HL,LAB.SER
	DAD	DE
	CMP	M		;SAME VOLUME ID -
	JNE	MFIFMT		;- NO, GO FORMAT
 
	MVI	A,LAB.NOD	;SET MAX VALID VOLUME TYPE
	LXI	HL,LAB.VLT
	DAD	DE
	CMP	M		;VOLUME TYPE VALID -
	JC	MFIFMT		;- NO, GO FORMAT
 
	MOV	A,B		;GET MEDIA BITS
	LXI	HL,LAB.VFL
	DAD	DE
	XRA	M		;GET MEDIA BITS DIFFERENCE
	ANI	VFL.80T+VFL.2SD  ;MEDIA CONFIGURATION BITS SAME -
	JNZ	MFIFMT		;- NO, GO FORMAT
 
MFIRFMT	CALL	$$TYPTX
	DB	NL,'Reformat <YES>?',' '+80H
	CALL	$$ITL.
	JC	MFIRFMT		;REPEAT IF CTL-D
 
	MOV	A,M
	ANA	A		;DEFAULT -
	JZ	MFIFMT		;- YES, GO FORMAT
 
	CALL	$$CNO		;'NO' -
	JE	MFINCHK		;- YES, SKIP FORMAT
 
	CALL	$$CYS		;'YES' -
	JNE	MFIRFMT		;- NO, REPEAT
 
MFIFMT	CALL	D.SDP		;SETUP DEVICE PARAMETERS
 
MFILP2	LDA	D.DLYHS		;GET HEAD LOAD DELAY COUNT
	ANA	A		;HEAD LOAD DELAY COUNT ZERO -
	JNZ	MFILP2		;- NO, WAIT
 
	XRA	A
	OUT	UP.FC		;SET FILL CHARACTER
	STA	D.TT		;SET INITIAL TRACK NUMBER
	MOV	B,A		;SET TRACK ZERO VOLUME ID
	MVI	A,MFISTEP/2	;*** REQUIRED TO CAUSE WHOLE TRACK ERASURE ***
	STA	D.MAIA		;SET SEEK STEP RATE FOR FORMATTING

MFILP3	CALL	D.SDT		;SEEK TRACK, SKIP SECTOR, DISABLE, SELECT SIDE
	LDA	D.DVCTL		;GET DEVICE CONTROL BITS
	ORI	DF.WG		;TURN ON WRITE
	OUT	DP.DC
 
	XRA	A
	STA	D.TS		;SET INITIAL SECTOR
	LDA	D.TT		;GET TRACK NUMBER
 
MFILP4	SUI	10		;COMPUTE TT MOD 10 - 10
	JNC	MFILP4
 
	MOV	H,A
	MVI	L,MFISKEW	;GET SKEW FACTOR
	XRA	A
 
MFILP5	ADD	H		;COMPUTE MFISKEW * (TT MOD 10 - 10)
	DCR	L
	JNZ	MFILP5
 
MFILP6	ADI	10		;COMPUTE (MFISKEW * TT) MOD 10
	JNC	MFILP6
 
	MOV	C,A		;SAVE SKEW FACTOR OF FIRST SECTOR ON TRACK
	INR	C		;INCREMENT SKEW FACTOR FOR COUNT LOOP
 
MFILP7	CALL	MFIWHOL		;GO WAIT FOR NEXT HOLE
	JNC	MFILP7		;NOT SHORT SECTOR, LOOP
 
MFILP8	CALL	MFIWHOL		;WAIT FOR BEGINNING OF NEXT SECTOR
	DCR	C		;ANY REMAINING SECTORS TO SKIP FOR SKEWING -
	JNZ	MFILP8		;- YES, LOOP
 
	MVI	A,1
	MVI	C,10
	CALL	D.WSP		;WRITE SYNC PATTERN
	MOV	A,B		;GET VOLUME ID
	CALL	D.WNB		;WRITE BYTE
	LDA	D.TT		;GET TRACK NUMBER
	CALL	D.WNB
	LDA	D.TS		;GET SECTOR NUMBER
	CALL	D.WNB
	MOV	A,D
	CALL	D.WNB		;WRITE CHECK BYTE
 
	MVI	A,1
	MVI	C,1+10
	CALL	D.WSP		;WRITE SYNC PATTERN
	MVI	C,#256		;SET SECTOR BYTE COUNT
 
MFILP9	MVI	A,-1		;WRITE ALL BITS FOR MEDIA CHECK
	CALL	D.WNB		;WRITE TEST PATTERN
	DCR	C
	JNZ	MFILP9		;WRITE 256
 
	MVI	C,4
 
MFILP10	XRA	A		;CHECKSUM/TRAILING BYTES ARE ZERO
	CALL	D.WNB		;WRITE CHECKSUM, LET TUNNEL GAP PASS
	DCR	C
	JNZ	MFILP10

	LDA	D.DVCTL		;GET DISK CONTROL BITS
	OUT	DP.DC		;TURN OFF WRITE
	MVI	C,1		;SET SKEW FACTOR FOR SUBSEQUENT SECTORS
	LXI	HL,D.TS		;GET SECTOR NUMBER LOC
	INR	M		;INCREMENT SECTOR NUMBER
	MOV	A,M		;GET NEW SECTOR
	CPI	10		;END OF TRACK -
	JC	MFILP8		;- NO, LOOP
 
	EI
	POP	BC		;SET VOLUME ID FOR REMAINING TRACKS
	PUSH	BC		;SAVE VOLUME ID, NUMBER OF TRACKS
	LXI	HL,D.TT		;GET TRACK NUMBER LOC
	INR	M		;INCREMENT TRACK NUMBER
	MOV	A,M		;GET NEW TRACK
	CMP	C		;END OF DISK -
	JC	MFILP3		;- NO, LOOP

	LHLD	D.VOLPT
	MOV	M,B		;SET VOLUME ID
	CALL	D.STZ		;SEEK TRACK ZERO
	CALL	D.XOK		;DESELECT DISK
 
MFIMCHK	CALL	$$TYPTX
	DB	NL,'Media check <NO>?',' '+80H
	CALL	$$ITL.
	JC	MFIMCHK		;REPEAT IF CTL-D
 
	MOV	A,M
	ANA	A		;DEFAULT -
	JZ	MFINCHK		;- YES, SKIP
 
	CALL	$$CNO		;'NO' -
	JE	MFINCHK		;- YES, SKIP
 
	CALL	$$CYS		;'YES' -
	JNE	MFIMCHK		;- NO, REPEAT
 
	CALL	$$TYPTX
	DB	ENL
	LHLD	VOLSIZ
	XCHG			;SAVE MAX BLOCK NUMBER + 1
 
MFIMLP1	LXI	BC,256
	LHLD	$$BUFF		;GET WORK BUFFER LOC
	XCHG			;(DE) = BUFFER LOC, (HL) = BLOCK NUMBER
	DCX	HL		;COMPUTE NEXT BLOCK NUMBER
	PUSH	HL		;SAVE BLOCK NUMBER
	PUSH	DE		;SAVE DATA LOC
	CALL	D.READ		;GO READ A BLOCK
	POP	HL		;RESTORE DATA LOC
	JC	MFIMERR		;- ERROR, GO LIST
 
	XRA	A
	MOV	C,A
	CMA
 
MFIMLP2	CMP	M		;CORRECT DATA -
	JNE	MFIMERR		;- NO, GO LIST
 
	INX	HL
	DCR	C		;ANY MORE DATA TO CHECK -
	JNZ	MFIMLP2		;- YES, LOOP
 
	JMP	MFIMTST		;DATA GOOD, CONTINUE
 
MFIMERR	POP	BC		;GET BLOCK NUMBER
	PUSH	BC
	LXI	HL,MFIMEM-4	;GET OUTPUT LOC
	MVI	A,4		;GET NUMBER OF DIGITS TO CONVERT
	CALL	$UDD		;GO CONVERT BLOCK NUMBER
	CALL	$$TYPTX
	DB	'Error in sector 0000'
MFIMEM	DB	ENL
 
MFIMTST	LXI	HL,-10-1
	POP	DE		;RESTORE BLOCK NUMBER
	DAD	DE
	JC	MFIMLP1
 
	LHLD	D.VOLPT		;GET VOLUME ID LOC
	MVI	M,0		;RESET VOLUME ID FOR TRACK ZERO
	MOV	A,E		;GET SECTOR NUMBER ON TRACK ZERO
	ANA	A		;END OF MEDIA CHECK -
	JNZ	MFIMLP1		;- NO, CONTINUE
 
MFINCHK	POP	BC		;DISCARD VOLUME ID, NUMBER OF TRACKS
	LHLD	D.VOLPT
	MVI	M,0		;RESET VOLUME ID
	LHLD	$$BUFF		;GET WORK BUFFER LOC
	PUSH	HL		;SAVE BUFFER LOC
	MOV	B,A		;SET CLEAR COUNT = 256
	CALL	$ZERO		;CLEAR LABEL RECORD AREA
 
	POP	DE		;RESTORE BUFFER LOC
	POP	BC		;RESTORE MEDIA BITS, VOLUME ID
 
	LXI	HL,LAB.SER
	DAD	DE
	MOV	M,C		;SET VOLUME ID IN TEMP LABEL
 
	LXI	HL,LAB.VLT
	DAD	DE
	MVI	M,LAB.NOD	;SET VOLUME TYPE
 
	LXI	HL,LAB.VFL
	DAD	DE
	MOV	M,B		;SET VOLUME FLAGS
 
	LXI	BC,256
	LXI	HL,9
	CALL	D.WRITE		;WRITE DUMMY LABEL RECORD
 
	RET			;EXIT
 
MFIWHOL	LXI	HL,0		;INITIALIZE COUNT
	LXI	DE,DF.HD	;GET ZERO, HOLE DETECT BIT
	CALL	MFIWCHG		;WAIT FOR NO HOLE
	MOV	D,E		;GET HOLE DETECT BIT
	CALL	MFIWCHG		;WAIT FOR HOLE
	LXI	DE,15*2048/35	;GET TIME FOR 15 MILLISECONDS
	DAD	DE		;SET CARRY IF SHORT SECTOR
	RET
 
MFIWCHG	DCX	HL		;(06) COUNT LOOP ITERATIONS
	IN	DP.DC		;(11) GET DISK STATUS
	ANA	E		;(04) GET HOLE STATUS
	CMP	D		;(04) CORRECT STATUS -
	JNE	MFIWCHG		;(10) - NO, WAIT
*				 =35
	LDA	D.WHDA		;GET POST-HOLE DELAY
	JMP	D.UDLY		;GO DELAY ACROSS HOLE TRANSITION

MFISEEK	SET	30		;DEFAULT ***MAXIMUM*** SEEK STEP TIME (MS)
	SPACE	3
